# First let's describe theoritical concept about the configuration.
#
# Platform:
# A platform is a pair [OS,CPU], e.g (Win,x64), (Win,x86), (Linux,x64), (Win,ARM) A same application can be built for different platform.
#
# Toolchain:
# A toolchain is a group of tool used to build application for a particular.
# Several toolchain can be compatible with the same platform, but one toolchain
# is only compatible with one platform.
#
# Platform SDK:
# Platform SDK, also known as system librairies, is a set of libraires, headers, etc. to program
# for the particular platform. It depends on one OS, but may contain several distribution for 
# different CPU and different toolchain.
#

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

###################
# Current Platform
###################

os_is_mac = false
os_is_win = false
if (current_os == "mac") {
  os_is_mac = true
} else if (current_os == "win") {
  os_is_win = true
} else {
  print("current_os=${current_os}")
  assert(false)
}

cpu_is_x64 = false
cpu_is_x86 = false
if (current_cpu == "x64") {
  cpu_is_x64 = true
} else if (current_cpu == "x86") {
  cpu_is_x86 = true
} else {
  print("current_cpu=${current_cpu}")
  assert(false)
}

platform_is_mac = false
platform_is_win_x64 = false
platform_is_win_x86 = false

if (os_is_mac) {
  platform_is_mac = true
} else if (os_is_win && cpu_is_x64) {
  platform_is_win_x64 = true
} else if (os_is_win && cpu_is_x86) {
  platform_is_win_x86 = true
}
assert(platform_is_mac || platform_is_win_x64 || platform_is_win_x86)

################
# Host Platform
################

host_os_is_mac = false
host_os_is_win = false
if (host_os == "mac") {
  host_os_is_mac = true
} else if (host_os == "win") {
  host_os_is_win = true
} else {
  print("host_os=${host_os}")
  assert(false)
}

host_cpu_is_x64 = false
host_cpu_is_x86 = false
if (host_cpu == "x64") {
  host_cpu_is_x64 = true
} else if (host_cpu == "x86") {
  host_cpu_is_x86 = true
} else {
  print("host_cpu=${host_cpu}")
  assert(false)
}

host_platform_is_mac = false
host_platform_is_win_x64 = false
host_platform_is_win_x86 = false

if (os_is_mac) {
  host_platform_is_mac = true
} else if (os_is_win && cpu_is_x64) {
  host_platform_is_win_x64 = true
} else if (os_is_win && cpu_is_x86) {
  host_platform_is_win_x86 = true
}
assert(host_platform_is_mac || host_platform_is_win_x64 || host_platform_is_win_x86)

#############
# Toolchains
#############

if (host_os_is_mac) {
  toolchain_mac_clang = "//build/gn/platform/mac:clang"
  toolchain_mac_gcc = "//build/gn/platform/mac:gcc"
} else if (host_os_is_win) {
  toolchain_win_x64_msvc = "//build/gn/platform/win_x64:msvc"
  toolchain_win_x86_msvc = "//build/gn/platform/win_x86:msvc"
}

if (platform_is_mac) {
  set_default_toolchain("$toolchain_mac_clang")
} else if (platform_is_win_x64) {
  set_default_toolchain("$toolchain_win_x64_msvc")
} else if (platform_is_win_x86) {
  set_default_toolchain("$toolchain_win_x86_msvc")
}