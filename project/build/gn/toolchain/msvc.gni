import("//build/gn/ninja.gni")

touch_path = rebase_path("touch.py", root_build_dir)

template("msvc_toolchain") {
  env = "msvc_environment." + invoker.toolchain_id
  env_path = rebase_path(root_build_dir + "/" + env)
  
  msvc_config = exec_script("//build/script/find_msvc.py", [ "2017", invoker.cpu, ], "scope")
  exec_script("//build/script/setup_msvc_environment.py", [ msvc_config.bin, env_path,], "scope")

  toolchain(target_name) {
    lib_switch = "" # built-in variable: How libraries input should be expanded
    lib_dir_switch = "/LIBPATH:" # build-in variable: How libraries directory should be expanded

    tool("cxx") {
      rspfile = "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.rsp"
      pdbname = "{{target_out_dir}}/{{target_output_name}}.{{source_name_part}}.pdb"
      command = "$ninja -t msvc -e $env -- cl.exe /nologo /EHsc /showIncludes /FC @$rspfile /c {{source}} /Fo{{output}} /Fd$pdbname"
      depsformat = "msvc"
      description = "CXX {{output}}"
      outputs = [
        "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.obj",
      ]
      rspfile_content = "{{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}}"
    }

    tool("link") {
      outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
      pdbfile = "{{output_dir}}/{{target_output_name}}.pdb"
      rspfile = "$outfile.rsp"
      command = "$ninja -t msvc -e $env -- link.exe /nologo /OUT:$outfile /PDB:$pdbfile @$rspfile"
      description = "LINK {{label}}"
      default_output_dir = "out/${invoker.toolchain_id}/bin"
      rspfile_content = "{{ldflags}} {{solibs}} {{libs}} {{inputs}}"
      outputs = [
        outfile
      ]
      default_output_extension = ".exe"
    }

    tool("alink") {
      rspfile = "{{output}}.rsp"
      command = "$ninja -t msvc -e $env -- lib.exe /nologo /OUT:{{output}} @$rspfile"
      description = "LIB {{output}}"
      outputs = [
        "{{target_out_dir}}/{{target_output_name}}{{output_extension}}",
      ]
      default_output_extension = ".lib"

      rspfile_content = "{{inputs_newline}}"
    }

    tool("stamp") {
      command = "python.exe $touch_path {{output}}"
      description = "STAMP {{output}}"
    }

    toolchain_args = {
        current_os = invoker.os
        current_cpu = invoker.cpu

        target_os = target_os
        target_cpu = target_cpu
    }
  }
}